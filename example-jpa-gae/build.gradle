/**
 * 
 * Copyright (C) 2011-2014 Martin Goellnitz
 * 
 * This work is licensed under the Creative Commons Attribution 3.0 
 * Unported License. To view a copy of this license, visit 
 * http://creativecommons.org/licenses/by/3.0/ or send a letter to 
 * Creative Commons, 444 Castro Street, Suite 900, Mountain View, 
 * California, 94041, USA.
 * 
 */
defaultTasks 'clean', 'build'

def tangram_version = '1.0-SNAPSHOT'

// Switch to have the options in one example project - optional external override
def tangram_backend = project.hasProperty('backend') ? "$backend" : 'dinistiq' // spring or dinistiq

buildscript {
  repositories {
    mavenCentral()
    maven { url "http://repository-tangram.forge.cloudbees.com/snapshot" }
  }
  dependencies {
    classpath 'tangram:gradle-plugin:1.0-SNAPSHOT'
  }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'maven'
apply plugin: 'jetty'
apply plugin: 'tangram'

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar.enabled = true 

repositories {
  // most of the usual stuff
  mavenCentral()
  // the tangram snapshots
  maven { url "http://repository-tangram.forge.cloudbees.com/snapshot" }
  // the tangram stuff
  maven { url "http://my-amor.appspot.com/repository" }
}

configurations {
  libs
  // this is only relevant for spring scenarios since spring still uses jcl
  all*.exclude module: 'commons-logging'
}

artifacts {
  archives war
  libs jar
}

dependencies {
  webapp "tangram:tangram-gae:$tangram_version:war@war"
  // compile "tangram:tangram-gae:$tangram_version"
  compile "tangram:tangram-$tangram_backend:$tangram_version"
  
  webapp "tangram:tangram-editor:$tangram_version:war@war"
  runtime "tangram:tangram-editor:$tangram_version"

  compile "org.slf4j:slf4j-jdk14:$versions.slf4j"  
  
  compile "tangram:tangram-jpa:$tangram_version:nucleus"
  compile "org.datanucleus:datanucleus-api-jpa:$versions.datanucleus"
    
  testCompile "junit:junit:$versions.junit"
  
  providedCompile "$versions.servlet_api"
  providedCompile "$versions.jsp_api"
}

clean.doFirst {
  copy {
    from "$buildDir/war/WEB-INF/appengine-generated"
    into 'local-db'
    include '**/**'
  }
}

jar {
  if (tangram_backend == 'spring') {
    excludes = [ 'dinistiq/**' ]
  }
}

war {
  archiveName = "${project.name}-${tangram_backend}.war"
  webXml = file("src/main/webapp/WEB-INF/web-${tangram_backend}.xml") 
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  if (tangram_backend == 'spring') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/shiro.ini', 'shiro/**' ]
  } else {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/tangram/**', 'WEB-INF/security-*.xml' ]
  }
}

// to use the local dev_appserver in this directory and to copy the db files back and forth
war.doLast {
  ant.unzip(src: war.archivePath, dest: "$buildDir/war")
  copy {
    from 'local-db'
    into "$buildDir/war/WEB-INF/appengine-generated"
    include '**/**'
  }
}

task appserverRun (type :Exec) {
  if (System.properties['os.name'].toLowerCase().contains('windows')) {
    commandLine 'cmd', '/c', 'dev_appserver --jvm_flag=-Ddatastore.default_high_rep_job_policy_unapplied_job_pct=1 --disable_update_check -p 12380 build\\war'
  } else {
    commandLine 'bash', '-c', 'dev_appserver.sh --jvm_flag=-Ddatastore.default_high_rep_job_policy_unapplied_job_pct=1 --disable_update_check -p 12380 build/war'
  } // if
}

// For the users of a the legacy IDE Eclipse
// apply plugin: 'eclipse'
// Project specific default output directory
// eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }
