/**
 *
 * Copyright (C) 2011-2015 Martin Goellnitz
 *
 * This work is licensed under the Creative Commons Attribution 3.0
 * Unported License. To view a copy of this license, visit
 * http://creativecommons.org/licenses/by/3.0/ or send a letter to
 * Creative Commons, 444 Castro Street, Suite 900, Mountain View,
 * California, 94041, USA.
 *
 */
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    // Cannot work with the version labels from the tangram plugin here
    classpath 'tangram:gradle-plugin:1.0.0'
  }
}

def tangram_version = '1.0.0'

// When changing the database backend don't forget to switch the files
// jpaConfigOverrides to point to the correct database file.

// Some switches to have all options in one example project - with optional external override
def tangram_backend = project.hasProperty('backend') ? "$backend" : 'dinistiq' // spring, dinistiq, guicy
def jpa_backend = project.hasProperty('jpa') ? "$jpa" : 'openjpa' // openjpa, eclipselink, hibernate, or datanucleus
// JPA can be used with or without byte code transformation except for DataNucleus where it is always necessary.
def do_bytecode_transform = !project.hasProperty('noenhance')
// If you compile tangram yourself, you might want to try your local repo
def useLocalRepo = project.hasProperty('local')

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

defaultTasks 'clean', 'build'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  if (useLocalRepo) {
    mavenLocal()
  }
  // Everything we use can be found here
  jcenter()
}

configurations {
  libs
  // this is only relevant for spring scenarios since spring still uses jcl
  all*.exclude module: 'commons-logging'
}

artifacts {
  archives war
  libs jar
}

dependencies {
  webapp "tangram:tangram-core:$tangram_version:war@war"
  compile "tangram:tangram-core:$tangram_version"

  // For Yahoo OpenID example
  compile "org.pac4j:pac4j-openid:$versions.pac4j"

  /* For JPA with DataNucleus */
  if (jpa_backend == 'datanucleus') {
    // DataNucleus does not work without byte-code transformation.
    compile "tangram:tangram-jpa:$tangram_version:nucleus"
    compile "org.datanucleus:datanucleus-api-jpa:$versions.datanucleus"
    compile "org.datanucleus:datanucleus-core:$versions.datanucleus"
    compile "$versions.jdo_api"
    compile "$versions.persistence_api"
    runtime "org.datanucleus:datanucleus-rdbms:$versions.datanucleus"
  }

  /* EclipseLink */
  if (jpa_backend == 'eclipselink') {
    if (do_bytecode_transform) {
      compile "tangram:tangram-jpa:$tangram_version:eclipselink"
    } else {
      compile "tangram:tangram-jpa:$tangram_version"
    }
    compile "org.eclipse.persistence:org.eclipse.persistence.jpa:$versions.eclipselink"
    compile "$versions.persistence_api"
  }

  // For JPA with Hibernate
  if (jpa_backend == 'hibernate') {
    if (do_bytecode_transform) {
      compile "tangram:tangram-jpa:$tangram_version:hibernate"
    } else {
      compile "tangram:tangram-jpa:$tangram_version"
    }
    compile "org.hibernate:hibernate-core:$versions.hibernate"
    compile "org.hibernate:hibernate-entitymanager:$versions.hibernate"
    compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
  }

  /* OpenJPA */
  if (jpa_backend == 'openjpa') {
    if (do_bytecode_transform) {
      compile "tangram:tangram-jpa:$tangram_version:openjpa"
    } else {
      compile "tangram:tangram-jpa:$tangram_version"
    }
    compile ("org.apache.openjpa:openjpa:$versions.openjpa") {
      exclude group: 'asm'
    }
  }

  runtime "tangram:tangram-${tangram_backend}:$tangram_version"

  webapp "tangram:tangram-editor:$tangram_version:war@war"
  runtime "tangram:tangram-editor:$tangram_version"

  /* For the FTP Service
  runtime "tangram:tangram-ftp:$tangram_version"
   */

  /* dinistiq */
  if (tangram_backend == 'dinistiq') {
  }
  /* springframework */
  if (tangram_backend == 'spring') {
  }
  /* Google Guice */
  if (tangram_backend == 'guicy') {
    compile ("com.mycila.guice.extensions:mycila-guice-jsr250:$versions.mycila_guice") {
      exclude module: 'sisu-guice'
    }
    runtime "org.slf4j:jcl-over-slf4j:$versions.slf4j"
    // dummies for compile scope to keep IDEs calm about groovy scripts
    compile"tangram:tangram-${tangram_backend}:$tangram_version"
  }

  runtime "hsqldb:hsqldb:1.8.0.10"
  runtime "org.slf4j:slf4j-log4j12:$versions.slf4j"

  testCompile "junit:junit:$versions.junit"

  // your container will have this for you
  providedCompile "$versions.servlet_api"
  providedCompile "$versions.jsp_api"
}

jar {
  if (tangram_backend == 'spring') {
    excludes = [ 'dinistiq/**', 'guicy/**' ]
  }
  if (tangram_backend == 'guicy') {
    excludes = [ 'dinistiq/**' ]
  }
  if (tangram_backend == 'dinistiq') {
    excludes = [ 'guicy/**' ]
  }
  // Use backend specific persistence.xml for JPA
  // In this example use the same pesistence.xml which was used for enhancing or weaving
  // For EclipseLink you might want to use two separate files though.
  def jpa_method = jpa_backend
  if (jpa_backend == 'openjpa') jpa_method='enhance'
  if (jpa_backend == 'eclipselink') jpa_method='weave'
  into ('') {
    from "$project.projectDir/$jpa_method"
    include 'META-INF/persistence.xml'
  }
}

war {
  def index = jpa_backend.indexOf('i') > 0 ? jpa_backend.length()-4 : 4
  archiveName = "${project.name}-${jpa_backend[0]}${jpa_backend[index]}-${tangram_backend}.war"
  webXml = file("src/main/webapp/WEB-INF/web-${tangram_backend}.xml")
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  if (tangram_backend == 'spring') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml' ]
  }
  if (tangram_backend == 'dinstiq') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/tangram/**' ]
  }
  if (tangram_backend == 'guicy') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/tangram/**' ]
  }
}

stopPort = 12390
httpPort = 12380

// If you are still using eclipse, you might want to use these
// apply plugin: 'eclipse'
// Project specific default output directory
// eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }
