/**
 *
 * Copyright (C) 2011-2015 Martin Goellnitz
 *
 * This work is licensed under the Creative Commons Attribution 3.0
 * Unported License. To view a copy of this license, visit
 * http://creativecommons.org/licenses/by/3.0/ or send a letter to
 * Creative Commons, 444 Castro Street, Suite 900, Mountain View,
 * California, 94041, USA.
 *
 */
import org.gradle.api.file.FileCopyDetails
import org.gradle.api.file.FileTree
import org.gradle.api.file.RelativePath
import de.undercouch.gradle.tasks.download.Download
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/artifactory/oss-snapshot-local" }
    jcenter()
  }
  dependencies {
    // Cannot work with the version labels from the tangram plugin here
    classpath 'tangram:gradle-plugin:1.1-SNAPSHOT'
    classpath 'org.akhikhl.gretty:gretty:1.2.4'
    classpath 'de.undercouch:gradle-download-task:2.0.0'
  }
}

def tangram_version = '1.1-SNAPSHOT'
// Switch to have the options in one example project - optional external override
def tangram_backend = project.hasProperty('backend') ? "$backend" : 'guicy' // spring, dinistiq, guicy
// If you compile tangram yourself, you might want to try your local repo
def use_local_repo = project.hasProperty('local')
// Swith the servlet container to be used by the gretty plugin
def servlet_container = project.hasProperty('container') ? "$container" : 'jetty9' // jetty7, jetty8', jetty9, tomcat7, tomcat8
// The log directory to be used by gretty and tangram itself - only suitable for development with this default
def log_dir = "./build" // /var/log/tangram

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'tangram'
apply plugin: 'de.undercouch.download'
// Use the plugin section to integrate this if you are not in a multiproject environment
apply plugin: 'org.akhikhl.gretty'

defaultTasks 'clean', 'build'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  if (use_local_repo) {
    mavenLocal()
  }
  maven { url "http://oss.jfrog.org/artifactory/oss-snapshot-local" }
  // Everything we use can be found here
  jcenter()
}

configurations {
  libs
  // this is only relevant for spring scenarios since spring still uses jcl
  all*.exclude module: 'commons-logging'
}

artifacts {
  archives war
  libs jar
}

sourceSets {
  main {
    java {
      srcDir "src/main/java"
      srcDir "src/main/java-$tangram_backend"
    }
  }
}

dependencies {
  // You can also add the core webapp files instead them from the jar
  // webapp "tangram:tangram-core:$tangram_version:war@war"
  // You can also add the coma webapp files instead them from the jar
  // webapp "tangram:tangram-coma:$tangram_version:war@war"
  compile "tangram:tangram-coma:$tangram_version"

  compile "tangram:tangram-${tangram_backend}:$tangram_version"

  runtime "ch.qos.logback:logback-classic:$versions.logback"
  runtime "mysql:mysql-connector-java:5.1.36"

  testCompile "org.testng:testng:$versions.testng"

  // your container will have this for you
  providedCompile "$versions.servlet_api"
  providedCompile "$versions.jsp_api"
}

processResources {
  filter(ReplaceTokens, tokens: [app: project.name.toString(), logdir: log_dir.toString()])
}

test {
  useTestNG() {
  }
}

jar {
  if (tangram_backend == 'spring') {
    excludes = [ 'dinistiq/**', 'guicy/**' ]
  }
  if (tangram_backend == 'dinistiq') {
    excludes = [ 'guicy/**' ]
  }
  if (tangram_backend == 'guicy') {
    excludes = [ 'dinistiq/**' ]
  }
}

/**
 *  coremediaUsername and coremediaPassword must be specified in your personal
 *  gradle.properties file in your ${GRADLE_USER_HOME}. Ask the friendly
 *  coremedia support staff for details.
 */
war {
  archiveName = "${project.name}-${tangram_backend}.war"
  // web.xml is included by the gretty plugin to be able to use "appRun" and not only "appRunWar"
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  if (tangram_backend == 'spring') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml' ]
  } else {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml' ]
  }

  def examplesFile = new File("$projectDir/cap-examples.jar")

  if ((!examplesFile.exists()) && project.hasProperty("coremediaUsername")) {
    download {
      src 'https://releases.coremedia.com/cmv/16/cms/5.2.1456/cap-examples.jar'
      dest "$projectDir"
      onlyIfNewer true
      username coremediaUsername
      password coremediaPassword
    }
  } // if

  if (examplesFile.exists()) {
    FileTree cmexamples = zipTree("$projectDir/cap-examples.jar")

    // complicated stuff until http://issues.gradle.org/browse/GRADLE-3025 gets solved

    // map css into correct target path
    into('') {
      from cmexamples
      include 'cae/menusite/src/css/**'
      eachFile { FileCopyDetails fcp ->
        if (fcp.relativePath.pathString.startsWith('cae/menusite/src/css')) {
          def segments = fcp.relativePath.segments
          def pathsegments = segments[3..-1] as String[]
          fcp.relativePath = new RelativePath(!fcp.file.isDirectory(), pathsegments)
        } else {
          fcp.exclude()
        } // if
      } // eachFile
    } // into

    // map templates into correct target path for standard tangram file layout
    into('WEB-INF') {
      from cmexamples
      include 'cae/menusite/src/templates/**'
      eachFile { FileCopyDetails fcp ->
        if (fcp.relativePath.pathString.startsWith('WEB-INF/cae/menusite/src/templates')) {
          def segments = fcp.relativePath.segments
          def pathsegments = segments[0,3..-1] as String[]
          pathsegments[1] = 'view'
          pathsegments[2] = 'jsp'
          fcp.relativePath = new RelativePath(!fcp.file.isDirectory(), pathsegments)
        } else {
          fcp.exclude()
        } // if
      } // eachFile
    } // into
  } // if
}

gretty {
  servletContainer = "$servlet_container"
  logDir = "$log_dir"
  logFileName = "$servlet_container"
  consoleLogEnabled = false
  httpsEnabled=true
  httpPort=12380
  httpsPort=12343
  servicePort=12390
  statusPort=12391
  webappCopy {
    into ('WEB-INF') {
      from "$project.projectDir/src/main/webapp/WEB-INF/web-${tangram_backend}.xml"
      rename("web-${tangram_backend}.xml", "web.xml")
    }
  }
}

// For the users of a the legacy IDE Eclipse
// apply plugin: 'eclipse'
// Project specific default output directory
// eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }
