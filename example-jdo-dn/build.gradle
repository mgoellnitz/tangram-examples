/**
 * 
 * Copyright (C) 2011-2014 Martin Goellnitz
 * 
 * This work is licensed under the Creative Commons Attribution 3.0 
 * Unported License. To view a copy of this license, visit 
 * http://creativecommons.org/licenses/by/3.0/ or send a letter to 
 * Creative Commons, 444 Castro Street, Suite 900, Mountain View, 
 * California, 94041, USA.
 * 
 */
defaultTasks 'clean', 'build'

ext.tangram_version = '0.9-SNAPSHOT'
ext.tangram_backend = 'dinistiq' // spring or dinistiq

buildscript {
  repositories {
    mavenCentral()
    maven { url "http://repository-tangram.forge.cloudbees.com/snapshot" }
  }
  dependencies {
    classpath "tangram:gradle-plugin:0.9-SNAPSHOT"
  }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'maven'
apply plugin: 'jetty'
apply plugin: 'tangram'

sourceCompatibility = 1.7
targetCompatibility = 1.7

// don't let gradle work with its defaults on my files...
webAppDirName = 'disabled'
jar.enabled = true 

repositories {
  // most of the usual stuff
  mavenCentral()
  // the tangram snapshots
  maven { url "http://repository-tangram.forge.cloudbees.com/snapshot" }
  // the tangram releases
  maven { url "http://my-amor.appspot.com/repository" }
}

configurations {
  libs 
  webapp
  // see explanation in dependencies section when this will be necessary
  // reallyNeeded
}

artifacts {
  archives war
  libs jar
}

/* Spring security framework */
if (tangram_backend.equals("dinistiq")) {
  ext.security_core = "org.apache.shiro:shiro-core:$versions.shiro"
  ext.security_web = "org.apache.shiro:shiro-web:$versions.shiro"
}
/* Apache Shiro most likely to be used together with dinistiq */
if (tangram_backend.equals("spring")) {
  ext.security_core = "org.springframework.security:spring-security-config:$versions.springsecurity"
  ext.security_web = "org.springframework.security:spring-security-web:$versions.springsecurity"
}

dependencies {
  webapp "tangram:tangram-core:$tangram_version:war@war"  
  compile "tangram:tangram-nucleus:$tangram_version"
  // e.g. rdbms of datanucleus and any db driver you'd like and which is supported
  runtime "org.datanucleus:datanucleus-rdbms:$versions.datanucleus"  

  runtime "tangram:tangram-${tangram_backend}:$tangram_version"
  
  // mongodb
  // runtime "org.datanucleus:datanucleus-mongodb:$versions.datanucleus"
  // flat file formats
  // runtime "org.datanucleus:datanucleus-xml:$versions.datanucleus"
  // runtime "org.datanucleus:datanucleus-odf:$versions.datanucleus"
  // runtime "org.datanucleus:datanucleus-excel:$versions.datanucleus"
  
  webapp "tangram:tangram-editor:$tangram_version:war@war"  
  runtime "tangram:tangram-editor:$tangram_version"

  /* For the FTP Service
  runtime "tangram:tangram-ftp:$tangram_version"
   */
    
  runtime "$security_core"
  runtime "$security_web"
  
  // This fixes a problem with the WAR lib directory calculation needed for run@cloudbees:
  // reallyNeeded "org.ow2.asm:asm:4.0"
  
  runtime "hsqldb:hsqldb:1.8.0.10"
  runtime "org.slf4j:slf4j-log4j12:$versions.slf4j"  
  // compile "mysql:mysql-connector-java:5.1.21"
  
  testCompile "junit:junit:$versions.junit"
  
  // your container will have this for you
  providedCompile "javax.servlet:servlet-api:$versions.servlet"
  providedCompile "javax.servlet:jsp-api:$versions.jsp"
}

compileJava.doLast { nucleusJdoEnhance() }

war.doFirst { overlayWebapp(war) }

war {
  webXml = file("src/main/webapp/WEB-INF/web-${tangram_backend}.xml") 
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  // see explanation in dependencies section when this will be needed alternatively for the line above
  // classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime + configurations.reallyNeeded
  excludes = [ "classes/**", "WEB-INF/web*.xml" ]
}

stopPort = 12390
httpPort = 12380

// If you are still using eclipse, you might want to use these
// apply plugin: 'eclipse'
// Project specific default output directory
// eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }
