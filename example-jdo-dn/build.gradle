/**
 * 
 * Copyright (C) 2011-2015 Martin Goellnitz
 * 
 * This work is licensed under the Creative Commons Attribution 3.0 
 * Unported License. To view a copy of this license, visit 
 * http://creativecommons.org/licenses/by/3.0/ or send a letter to 
 * Creative Commons, 444 Castro Street, Suite 900, Mountain View, 
 * California, 94041, USA.
 * 
 */
defaultTasks 'clean', 'build'

def tangram_version = '1.0-SNAPSHOT'

// Switch to have the options in one example project - optional external override
def tangram_backend = project.hasProperty('backend') ? "$backend" : 'dinistiq' // spring, dinistiq, guicy

buildscript {
  repositories {
    mavenCentral()
    maven { url "https://raw.githubusercontent.com/mgoellnitz/artifacts/master" }
  }
  dependencies {
    classpath 'tangram:gradle-plugin:1.0-SNAPSHOT'
  }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'tangram'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  // most of the usual stuff
  mavenCentral()
  // the tangram stuff
  maven { url "https://raw.githubusercontent.com/mgoellnitz/artifacts/master" }
}

configurations {
  libs
  // this is only relevant for spring scenarios since spring still uses jcl
  all*.exclude module: 'commons-logging'
  // see explanation in dependencies section when this will be necessary
  // reallyNeeded
}

artifacts {
  archives war
  libs jar
}

dependencies {
  webapp "tangram:tangram-core:$tangram_version:war@war"
  compile "tangram:tangram-nucleus:$tangram_version"
  // e.g. rdbms of DataNucleus and any db driver you'd like and which is supported
  runtime "org.datanucleus:datanucleus-rdbms:$versions.datanucleus"  

  runtime "tangram:tangram-${tangram_backend}:$tangram_version"
  
  // For MongoDB
  // runtime "org.datanucleus:datanucleus-mongodb:$versions.datanucleus"
  // Flat File Formats
  // runtime "org.datanucleus:datanucleus-xml:$versions.datanucleus"
  // runtime "org.datanucleus:datanucleus-odf:$versions.datanucleus"
  // runtime "org.datanucleus:datanucleus-excel:$versions.datanucleus"
  
  webapp "tangram:tangram-editor:$tangram_version:war@war"  
  runtime "tangram:tangram-editor:$tangram_version"

  /* For the FTP Service
  runtime "tangram:tangram-ftp:$tangram_version"
   */
    
  /* Apache Shiro most likely to be used together with dinistiq */
  if (tangram_backend == 'dinistiq') {
    runtime "org.apache.shiro:shiro-core:$versions.shiro"
    runtime "org.apache.shiro:shiro-web:$versions.shiro"
    runtime "org.slf4j:jcl-over-slf4j:$versions.slf4j"
  }
  /* Spring security framework */
  if (tangram_backend == 'spring') {
    runtime "org.springframework.security:spring-security-config:$versions.springsecurity"
    runtime "org.springframework.security:spring-security-web:$versions.springsecurity"
  }
  /* Google Guice needs has editor and shiro as compile paths so only logging is added here */
  if (tangram_backend == 'guicy') {
    runtime "org.slf4j:jcl-over-slf4j:$versions.slf4j"
  }
  
  // This fixes a problem with the WAR lib directory calculation needed for run@cloudbees:
  // reallyNeeded "org.ow2.asm:asm:4.0"
  
  runtime "hsqldb:hsqldb:1.8.0.10"
  runtime "org.slf4j:slf4j-log4j12:$versions.slf4j"  
  
  testCompile "junit:junit:$versions.junit"
  
  // your container will have this for you
  providedCompile "$versions.servlet_api"
  providedCompile "$versions.jsp_api"
  // dummies to keep IDEs calm about groovy scripts
  providedCompile "com.google.inject:guice:$versions.guice"
  if (tangram_backend == 'spring') {
    providedCompile "tangram:tangram-servlet:$tangram_version"
  }
}

jar {
  if (tangram_backend == 'spring') {
    excludes = [ 'dinistiq/**', 'guice/**' ]
  }
  if (tangram_backend == 'dinistiq') {
    excludes = [ 'guice/**' ]
  }
  if (tangram_backend == 'guicy') {
    excludes = [ 'dinistiq/**' ]
  }
}

war {
  archiveName = "${project.name}-${tangram_backend}.war"
  webXml = file("src/main/webapp/WEB-INF/web-${tangram_backend}.xml") 
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  // see explanation in dependencies section when this will be needed alternatively for the line above
  // classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime + configurations.reallyNeeded
  if (tangram_backend == 'spring') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/shiro.ini', 'shiro/**' ]
  }
  if (tangram_backend == 'dinstiq') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/tangram/**', 'WEB-INF/security-*.xml' ]
  }
  if (tangram_backend == 'guicy') {
    excludes = [ 'classes/**', 'WEB-INF/web*.xml', 'WEB-INF/tangram/**', 'WEB-INF/security-*.xml', 'WEB-INF/shiro.ini' ]
  }
}

stopPort = 12390
httpPort = 12380

// If you are still using eclipse, you might want to use these
// apply plugin: 'eclipse'
// Project specific default output directory
// eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }
