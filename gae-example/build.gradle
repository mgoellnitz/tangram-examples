defaultTasks 'clean', 'build'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext.tangram_version = '0.8-SNAPSHOT'

ext.servlet_spec = "2.5"
ext.jsp_spec = "2.0"
ext.junit_version = "4.10"

ext.httpPort = 12380

ext.datanucleus_enhancer_version = '3.1.1';

// don't let gradle work with its defaults on my files...
webAppDirName = 'disabled'

repositories {
  mavenLocal()
  // most of the usual stuff
  maven { url "http://repo1.maven.org/maven2" }
  // needed for latest datanucleus versions
  maven { url "http://www.datanucleus.org/downloads/maven2" }
  // the tangram stuff
  maven { url "http://my-amor.appspot.com/repository" }
  // needed for latest spring framework and security versions
  maven { url "http://repository.springsource.com/maven/bundles/release" }
}

jar.enabled = true 

// Project specific default output directory
eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }

configurations {
  libs 
  webapp
  enhance
}

artifacts {
  archives war
  libs jar
}

dependencies {
  webapp "tangram:tangram-gae:$tangram_version"
  
  compile "tangram:tangram-gae:$tangram_version"
  
  providedCompile "javax.servlet:servlet-api:$servlet_spec"
  providedCompile "javax.servlet:jsp-api:$jsp_spec"
  
  testCompile "junit:junit:$junit_version"

  enhance "org.apache.ant:ant:1.8.4"
  enhance "org.datanucleus:datanucleus-enhancer:$datanucleus_enhancer_version"
}

clean.doFirst {
    copy {
        from "$buildDir/war/WEB-INF/appengine-generated"
        into 'local-db'
        include '**/**'
    }
}

compileJava.doLast {
    try {
        ant.taskdef(name: 'enhance', classpath: configurations.enhance.asPath, classname: 'org.datanucleus.enhancer.tools.EnhancerTask')
        ant.enhance(failonerror: true, verbose: true, checkonly: false, dir: sourceSets['main'].output.classesDir.canonicalPath) {
            classpath {
                // for the log configuration...
                pathelement(path: '.')
                // The classes to be enhanced need to be on the class path
                pathelement(path: sourceSets['main'].output.classesDir.canonicalPath)
                // this is the real class path for the tool (s.a.)
                pathelement(path: sourceSets['main'].compileClasspath.asPath)
            }
            fileset(dir: sourceSets['main'].output.classesDir.canonicalPath)
        }
    } catch(Exception e) {
        println ''
        e.printStackTrace(System.out);
        throw new GradleException('An error occurred enhancing persistence capable classes.', e)
    }
}

war.doFirst {
  // Strange way of overwriting things - it must be the first webapp dependency
  if (configurations.webapp.dependencies.size() > 0) {
    String archiveFileName = configurations.webapp.asPath
    int idx = archiveFileName.indexOf(';')
    if (idx >= 0) {
      archiveFileName = archiveFileName.substring(0, idx)
    } // if
    println "unzipping $archiveFileName"
    ant.unzip(src: archiveFileName, dest: "$buildDir/target")  
  } // if
  
  // Missing: YUICompressor - see base system
  // It's not really that important since we don't have any
  // static Stylesheets or JavaScripts are in the examples.
  copy {
    from 'src/main/webapp'
    into "$buildDir/target"
    include '**/**'
  }
  
  into ('') {
    from "$buildDir/target"
    exclude 'WEB-INF/lib/tangram-**'
  }
}

war {
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  excludes = [ "classes/**" ]
}

// to use the local dev_appserver in this directory
war.doLast {
  ant.unzip(src: war.archivePath, dest: "$buildDir/war")
  copy {
    from 'local-db'
    into "$buildDir/war/WEB-INF/appengine-generated"
    include '**/**'
  }
}

// Sorry, just for windows for now
task runWar (type :Exec) {
    commandLine 'cmd', '/c', 'dev_appserver --jvm_flag=-Ddatastore.default_high_rep_job_policy_unapplied_job_pct=1 --disable_update_check -p 12380 build\\war'
}
